String androidGroup = 'Android Pre-Test Commands'

task listEmulators(type: Exec) {
    group androidGroup
    description 'lists all emulators on host machine'
    commandLine 'bash', '-c', 'avdmanager list avd | grep "Name"'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task createAndRunEmulatorAndroidM(dependsOn: listEmulators) {
    group androidGroup
    description 'starts emulator, creates and starts emulator if it does not exist (ANDROID-M)'
    String versionName = "ANDROID-M"
    String systemImageName = "system-images;android-23;google_apis;x86"
    doFirst {
        String existingEmulators = listEmulators.output()
        createEmulatorIfItDoesNotItExist(existingEmulators, versionName, systemImageName)
    }
    doLast {
        startEmulator(versionName)
    }
}

task createAndRunEmulatorAndroidN(dependsOn: listEmulators) {
    group androidGroup
    description 'starts emulator, creates and starts emulator if it does not exist (ANDROID-N)'
    String versionName = "ANDROID-N"
    String systemImageName = "system-images;android-25;google_apis_playstore;x86"
    doFirst {
        String existingEmulators = listEmulators.output()
        createEmulatorIfItDoesNotItExist(existingEmulators, versionName, systemImageName)
    }
    doLast {
        startEmulator(versionName)
    }
}

task createAndRunEmulatorAndroidO(dependsOn: listEmulators) {
    group androidGroup
    description 'starts emulator, creates and starts emulator if it does not exist (ANDROID-O)'
    String versionName = "ANDROID-O"
    String systemImageName = "system-images;android-27;google_apis_playstore;x86"
    doFirst {
        String existingEmulators = listEmulators.output()
        createEmulatorIfItDoesNotItExist(existingEmulators, versionName, systemImageName)
    }
    doLast {
        startEmulator(versionName)
    }
}

task listInstalledSDKPackages(type: Exec) {
    group androidGroup
    description ''
    commandLine 'bash', '-c', 'sdkmanager --list --channel=0'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString().split("Available Packages:")[0]
    }
}

task installSDKPackages(dependsOn: listInstalledSDKPackages) {
    String androidOImage = "system-images;android-27;google_apis_playstore;x86"
    String androidNImage = "system-images;android-25;google_apis_playstore;x86"
    String androidMImage = "system-images;android-23;google_apis;x86"
    group androidGroup
    doLast {
        String installedPackages = listInstalledSDKPackages.output()
        checkAndInstallSDKPackage(installedPackages, androidOImage)
        checkAndInstallSDKPackage(installedPackages, androidNImage)
        checkAndInstallSDKPackage(installedPackages, androidMImage)
    }
}

task launchAndroidMEmulator {
    dependsOn 'installSDKPackages'
    dependsOn 'createAndRunEmulatorAndroidM'
    tasks.findByName('createAndRunEmulatorAndroidM').mustRunAfter 'installSDKPackages'
}

task launchAndroidNEmulator {
    dependsOn 'installSDKPackages'
    dependsOn 'createAndRunEmulatorAndroidN'
    tasks.findByName('createAndRunEmulatorAndroidN').mustRunAfter 'installSDKPackages'
}

task launchAndroidOEmulator {
    dependsOn 'installSDKPackages'
    dependsOn 'createAndRunEmulatorAndroidO'
    tasks.findByName('createAndRunEmulatorAndroidO').mustRunAfter 'installSDKPackages'
}

def checkAndInstallSDKPackage(String installedPackages, String packageToInstall) {
    if(!installedPackages.contains(packageToInstall)) {
        println("Installing " + packageToInstall)
        exec {
            executable "sdkmanager"
            args packageToInstall
        }
    } else {
        println(packageToInstall + " is already installed")
    }
}

def createEmulatorIfItDoesNotItExist(String existingEmulators, String versionName, String systemImage) {
    println(existingEmulators)
    if(existingEmulators.contains(versionName)) {
        println("emulator " + versionName + " exists")
    } else {
        println("emulator not found, creating a new " + versionName + " emulator")
        exec {
            String command = 'echo no | avdmanager create avd -n "' + versionName + '" -c 1000M -k "' + systemImage + '"'
            commandLine 'bash', '-c', command
        }
    }
}

def startEmulator(String emulatorName) {
    println("starting " + emulatorName + " emulator")
    exec {
        String command = '"$(dirname "$(which emulator)")"/emulator -avd ' + emulatorName
        commandLine 'bash', '-c', command
    }
}